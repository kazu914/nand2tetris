// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // 命令のデコード
    Decode(instruction=instruction,a=a,c1=c1,c2=c2,c3=c3,c4=c4,c5=c5,c6=c6,d1=d1,d2=d2,d3=writeM,j1=j1,j2=j2,j3=j3);

    // A命令ならinstruction，C命令ならM
    Mux16(a=instruction,b=outMem, sel=instruction[15], out=inA);

    // A命令ならAレジスタに保存
    // C命令なら，d1ビットが立っていれば保存
    Not(in=instruction[15], out=isA);
    Or(a=d1, b=isA, out=loadA);
    ARegister(in=inA, load=loadA, out=A, out[0..14]=addressM);

    // C命令かつ，d2ビットが立っていればDレジスタに保存
    And(a=d2, b=instruction[15], out=loadD);
    DRegister(in=outMem, load=loadD, out=D);

    // C命令のaによりアドレスかアドレスのデータか
    Mux16(a=A, b=inM, sel=a, out=AM);

    ALU(x=D, y=AM, zx=c1, nx=c2, zy=c3, ny=c4, f=c5, no=c6, out=outM,out=outMem, zr=zr, ng=ng);

    // A命令ならジャンプしない
    // C命令ならJumpFlagが立ってれば飛ぶ
    // ジャンプしないならインクリメント
    Jumper(j1=j1, j2=j2, j3=j3, zr=zr, ng=ng, out=jumpFlag);
    And(a=jumpFlag, b=instruction[15], out=jump);
    Not(in=jump, out=inc);
    PC(in=A, load=jump, inc=inc, reset=reset, out[0..14]=pc);
}


